rootProject.name = 'bukkit-shade'

def collect = new ArrayList<String>()
def nmsNames = new File(rootProject.projectDir.absolutePath, "modules/versions/versions.csv")
println nmsNames.absolutePath

for (line in nmsNames.readLines()) {
    if(line.isEmpty())
        continue
    def list = line.split(',')
    def spigotVersion = list[0].trim()
    def nmsVersion = list[1]

    def modulePath = "versions/" + nmsVersion
    def fullModulePath = "modules/" + modulePath
    if (collect.contains(modulePath))
        continue

    collect.add(modulePath)
    def buildFile = new File(fullModulePath, "build.gradle")
    def srcFolder = new File(fullModulePath, "src/main/java/blue/sparse/bshade/versions/" + nmsVersion)
    if(!srcFolder.exists()) {
        srcFolder.mkdirs()
    }

    if (!buildFile.exists()) {
        buildFile.createNewFile()
        buildFile.write(String.format("""
            version '1.0-SNAPSHOT'

            dependencies {
                compile project(":versions")
                implementation fileTree("../jars/spigot-%s.jar")
            }

            jar {
                from configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
            }
        """.stripIndent(), spigotVersion))
    }
}

collect.addAll(["commands", "data", "util", "versions", "i18n", "math", "menu"])

collect.each {
    setupModule(it)
}

void setupModule(String str) {
    include "modules/$str"
    findProject(":modules/$str")?.name = str.split("/").last()
}
//include 'versions:1_14_R1'
//findProject(':versions:1_14_R1')?.name = '1_14_R1'

